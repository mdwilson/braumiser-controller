/*
brauduino semi automated single vessel RIMS
 created by s.mathison
 Copyright (C) 2012  Stephen Mathison
 Modified by Mike Wilson
 - Added INPUT_PULLUP to button inputs
 - swapped I/O ports for Buzz and Pump
 - changed to Fahrenheit
 - added heat and pump (H and P) indicators to the LCD
 - turned off pump during boil stage
 - added direct output% control and display during boil
 - saved boil heat % in EEPROM
 - 2v5 fixed hop addition when hoptime == boiltime
 - changed boil stage temp to 201 deg F.
 - 2v6  Added automated 2 tier control (no pump during mash in)
 - 2V7-2  Added delayed start timer
 - 2V7-3  Corrected hour display during start delay
 
 compiled on Arduino V1.0
 
 This program is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 (at your option) any later version.
 
 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.
 
 You should have received a copy of the GNU General Public License
 along with this program.  If not, see <http://www.gnu.org/licenses/>.*/



//libraries
#include <EEPROM.h>
#include <LiquidCrystal.h>
#include <OneWire.h>
#include <PID_v1.h>
#include <Serial.h>
OneWire ds(11);
LiquidCrystal lcd(2, 3, 4, 5, 6, 7);


// push buttons
const char Button_up = A3;
const char Button_dn = A2;
const char  Button_prev = A1;
const char  Button_nxt = A0;


// outputs
const int Heat = 9;
const int Pump = 10;
const int Buzz = 8;


// global variables
unsigned int WindowSize;
unsigned long windowStartTime;
unsigned long start;
double Setpoint, Input, Output,eepromKp, eepromKi, eepromKd;

//boolean autoLoop = false; 
boolean manualLoop = false;
boolean waterAdd = false;
boolean Conv_start = false;
boolean mpump = false;
boolean mheat = false;
boolean wtBtn = false;
boolean autoEnter = false;
boolean tempReached = false;
boolean pumpRest = false;
boolean resume = false;
boolean twovessel = false; //for 2 vessel automated mashin/mashout
float mset_temp = 95; //was 35C -mdw
float Temp_c, Temp_f, stageTemp,pumptempError; // added Temp_f -mdw
float boil_output = 100.0;  // boil output %
const float boilStageTemp = 201.0; //initial boil temp
int temp;
int x;
int  stageTime,hopTime;
byte mainMenu = 0;
byte pumpTime;
byte data[2];
byte second;
byte Busy = 0;
byte nmbrStgs;
byte nmbrHops;
byte tempHAddr;
byte tempLAddr;
byte timeAddr;
byte blhpAddr;
byte hopAdd;
byte boil_pcnt;
byte boilHeatAddr = 50;
byte twovesselAddr = 51;
byte startdelay15Addr = 52; //start delay time in 15 min increments

char* stgName[] ={
  "MashIn","Stage1","Stage2","Stage3","Stage4","Stage5","Stage6","Stage7","Stage8","Boil  "};
/*
// degree c sybmol 
 byte degc[8] =
 {
 B01000,
 B10100,
 B01000,
 B00011,
 B00100,
 B00100,
 B00011,
 B00000,
 };
 */
//deg f symbol
byte degf[8] =
{
  B01000,
  B10100,
  B01000,
  B01111,
  B01000,
  B01110,
  B01000,
  B01000,
};


//Specify the links and initial tuning parameters
PID myPID(&Input, &Output, &Setpoint,100,20,5, DIRECT);


//****** start of the funtions************** 


void Buzzer(int number)
{
  for (int i=0; i < number; i++)
  {
    digitalWrite (Buzz,HIGH);
    delay (500);
    digitalWrite(Buzz,LOW);
    delay(100);
  }

}



// Turn on heat and indicate on LCD - mdw)
void heat_on(void){
  digitalWrite (Heat,HIGH);
  display_lcd(0,0,"H");  
}

void heat_off(void){
  digitalWrite (Heat,LOW);
  display_lcd(0,0," ");  
}

void pump_on(void){
  digitalWrite(Pump,HIGH);
  display_lcd(0,1,"P"); 
}

void pump_off(void){
  digitalWrite(Pump,LOW);
  display_lcd(0,1," "); 
}



void pause_stage(void){
  boolean stage_pause = false;
  if (Button_1sec_press(Button_prev)){
    Buzzer(1);
    stage_pause = true;
    heat_off();
    pump_off();
    display_lcd(0,0,"     Paused    " );   
    while (stage_pause)
    {
      if (Button_1sec_press(Button_prev))stage_pause=false;
    }

  } 
}



void display_lcd (int pos , int line ,const char* lable){
  lcd.setCursor(pos,line);
  lcd.print(lable);
}



// 1 second button press
int Button_1sec_press (int Button_press){
  if (digitalRead(Button_press)==0){
    delay (1000);
    if (digitalRead(Button_press)==0){
      lcd.clear();
      while(digitalRead(Button_press)==0){
      }
      return 1;
    }
  }
  return 0;
}



// repeat button press
int Button_repeat (int Button_press){
  if (digitalRead(Button_press)==0){
    delay(200);
    return 1;
  }
  return 0;
}



// holds whilst button pressed 
int Button_hold_press (int Button_press){
  if (digitalRead (Button_press)==0){
    delay(50);
    while (digitalRead (Button_press)==0){
    }
    return 1;
  }
  return 0;
}



// reads the DS18B20 temperature probe 
void Temperature(void){
  ds.reset();
  ds.skip();
  // start conversion and return
  if (!(Conv_start)){
    ds.write(0x44,0);
    Conv_start = true;
    return;
  }
  // check for conversion if it isn't complete return if it is then convert to decimal
  if (Conv_start){
    Busy = ds.read_bit();
    if (Busy == 0){
      return;
    }
    ds.reset();
    ds.skip();
    ds.write(0xBE);  
    for ( int i = 0; i < 2; i++) {           // we need 2 bytes
      data[i] = ds.read();
    } 
    unsigned int raw = (data[1] << 8) + data[0];
    Temp_c = (raw & 0xFFFC) * 0.0625; 
    Temp_f = Temp_c * 1.8 +32;  //added Temp_f - mdw
    Conv_start = false;
    return;
  } 

}



// PID_HEAT modified to use SetMode(MANUAL)
void PID_HEAT (boolean autoMode){
  if (autoMode) {
    if ((Setpoint - Input)<11) //was 6, getting close, start feeding the PID -mdw
      myPID.Compute();
    if((Setpoint - Input)>9)   // was 5, ignore PID and go full speed -mdw
      Output=100;  // set the output to full on
  }
  // PWM the output
  unsigned long now = millis();
  if(now - windowStartTime>WindowSize)
  {                                     //time to shift the Relay Window
    windowStartTime += WindowSize;
  }
  if((Output*(WindowSize/100)) > now - windowStartTime) {
    heat_on();
  }
  else heat_off();

}



void load_pid_settings (void)
{
  eepromKp = word(EEPROM.read(0),EEPROM.read(1));// read the PID settings from the EEPROM
  eepromKi = word(EEPROM.read(2),EEPROM.read(3));
  eepromKd = word(EEPROM.read(4),EEPROM.read(5));
  eepromKi = eepromKi/100;
  myPID.SetTunings(eepromKp,eepromKi,eepromKd); // send the PID settings to the PID
  WindowSize = word(EEPROM.read(33),EEPROM.read(34));
  myPID.SetOutputLimits(0, 100);
  myPID.SetSampleTime(5000);

}  



boolean wait_for_confirm (boolean& test)
{ 
  wtBtn = true;
  while (wtBtn){               // wait for confirmation 
    if (Button_hold_press(Button_prev)){
      test = true;
      wtBtn = false;
      lcd.clear();
    }
    if (Button_hold_press(Button_nxt)){
      test = false;
      wtBtn = false;
      lcd.clear();

    }
  }
}



// change between upper and lower limit using up down buttons
float change_up_dwn(float& temp_change,int upper_limit,int lower_limit)
{
  // Increase set temp
  if (Button_repeat(Button_up)){
    temp_change++; 
    if (temp_change > upper_limit)temp_change = upper_limit;
  }
  // decrease temp
  if (Button_repeat(Button_dn)) 
  {
    temp_change--;
    if ( temp_change < lower_limit) temp_change = lower_limit;
  }

}



void quit_mode (boolean& processLoop)
{
  if ((digitalRead(Button_dn)==0) && (digitalRead(Button_up)==0)){
    heat_off();
    pump_off();
    processLoop = false;
    lcd.clear();
  }

}



void heat_control(void)
{
  //turns heat on or off      
  if (Button_hold_press(Button_prev)){
    if (mheat==false){
      mheat = true;
      windowStartTime = millis();
    }
    else{
      mheat = false;
      heat_off();
    }
  }

}



void pump_control(void)
{
  //turns the pump on or off
  if (Button_hold_press(Button_nxt)){
    if (mpump == false){
      mpump = true;
      pump_on();
    }
    else{
      mpump = false;
      pump_off();
    }
  }

}



void prompt_for_water (void){
  display_lcd(0,0,"  Water added?  ");
  Buzzer(3);
  display_lcd(0,1,"   Ok      Quit");

}



void pump_prime(void)
{
  lcd.clear();
  lcd.print("  Pump  Prime  ");  // priming the pump
  pump_on();
  delay (1000);
  pump_off();
  delay(200);
  pump_on();
  delay (1000);
  pump_off();
  delay(200);
  pump_on();
  delay (1000);
  pump_off();
  lcd.clear(); 

}



void pump_rest (int stage)
{
  if (stage==9 || (twovessel && stage==0)){
    pump_off();
    pumpRest =false;
  }
  else{
    if ((pumpTime < 10)){ // starts pumps and heat
      pump_on();
      pumpRest =false; 
    }
    if ((pumpTime >= 10)){ // pump rest
      pump_off();
      heat_off();
      pumpRest = true;
      if(pumpTime>=12 || ((stageTemp-Temp_f) > 1.0))pumpTime = 0;
    } 
  }
}



void check_for_resume(void){
  if(EEPROM.read(35)){ // read the auto started byte to see if it has been set and if so ask to resume

    display_lcd (0,0," Resume Process?");
    display_lcd (0,1,"  Yes      No");
    wait_for_confirm(resume);
    if(resume==true){
      tempHAddr = (EEPROM.read(36)*3)+6;
      tempLAddr = tempHAddr+1;
      timeAddr = tempHAddr+2;
      x = EEPROM.read(36);
      autoEnter = true;
      lcd.clear();

    }
  } 
}



void load_stage_settings (void){
  tempHAddr = 6; // setup initial stage addresses 
  tempLAddr = 7;
  timeAddr = 8;
  nmbrStgs = EEPROM.read(38);// read the number of steps
  nmbrHops = EEPROM.read(39);//read the number of hop additions
  twovessel = EEPROM.read(twovesselAddr);

}



void start_time (void)
{
  start = millis();
  // windowStartTime = millis();
  second = 0;
  // minute = 0;

}



void stage_timing (int stage)
{
  if ((millis()-start)>1000){  // timing routine
    start = millis();
    second++;
    if(!(tempReached))second=0;// starts counting down when temp reached
    if (second>59){
      display_lcd(11,0,"     ");  //was 10,0 - mdw
      second = 0;
      pumpTime++;
      if(stage == 0)pumpTime = 0;
      stageTime--;
      EEPROM.write(37,lowByte(stageTime));// saves stage time in case of interruption
    }
  }

}

// Timing routine added to allow delayed start for 2-vessel brewing -mdw
void startdelay_timing (int &min_remain)
{
  static int second;
  static unsigned long start;
  if ((millis()-start)>1000){  // timing routine
    start = millis();
    second++;
    if (second>59){
      //     display_lcd(11,0,"     ");  //was 10,0 - mdw
      second = 0;
      min_remain--;
      //      EEPROM.write(37,lowByte(stageTime));// saves stage time in case of interruption
    }
  }

}

// Display Count Down time and delay start
void startdelayloop (void)
{
  int startdelay;
  int lasttime;
  int hours, minutes;
  // get time in 15 minute increments from eeprom
  startdelay = EEPROM.read(startdelay15Addr) * 15;
  lasttime = startdelay;
  hours = startdelay / 60;
  minutes = startdelay % 60;
  //display updated time
  display_lcd(0,0," Time till Start");
  display_lcd(0,1,"                ");
  lcd.setCursor(4,1);
  lcd.print(hours);
  display_lcd(6,1, ":");
  lcd.print(minutes);
  while (autoEnter && (startdelay>0)) {
    startdelay_timing (startdelay); //check time
    if (startdelay < lasttime) {  // do on every minute tick
      lasttime = startdelay;
      hours = startdelay / 60;
      minutes = startdelay % 60;
      //display updated time
      display_lcd(0,0," Time till Start");
      display_lcd(0,1,"                ");
	  lcd.setCursor(4,1);
      lcd.print(hours);
      display_lcd(6,1, ":");
      lcd.print(minutes);
    }	
    quit_mode(autoEnter);
  }
    lcd.clear();
}

void hop_add (void)
{
  if(hopAdd <= nmbrHops){
    if (stageTime == hopTime){
      Buzzer(3);
      lcd.clear();
      lcd.print("    Add  Hops");
      delay(2000);
      Buzzer(3);
      hopAdd++;
      EEPROM.write(49,hopAdd);
      blhpAddr++;
      hopTime = EEPROM.read(blhpAddr);
      lcd.clear();
    }
  } 
}



void stage_loop (int stage, float H_temp=176, float L_temp=86){  //was 80C and 30C -mdw
  int lastminute;
  while ((stageTime>0)&&(autoEnter)){
    lastminute=stageTime;
    stage_timing(stage);
    Temperature();// get temp
    Setpoint = stageTemp;//
    Input = Temp_f; //was Temp_c -mdw
    pause_stage();
    if (pumpRest){
      display_lcd(0,0,"   Pump  Rest   ");
      display_lcd(0,1,"                ");
    }
    else{
      display_lcd(2,0,stgName[stage]);   
      display_lcd(8,0,"    ");  //was 6,0 -mdw
      display_lcd(11,0,"T=");
      lcd.print(stageTime);

      if (stage ==9 && tempReached) {  //if temp reached during boil
        change_up_dwn(boil_output,100,0);  // allow manual control of Output
        Output = boil_output;
        display_lcd(2,1,"%/A=");    //Display output%
        lcd.print(boil_output,0); //Display output%
        lcd.print(" "); //added space as place holder for <100
        PID_HEAT(false); //set heat in manual mode
      }
      else{
        change_up_dwn(stageTemp,248,68);
        display_lcd(2,1,"S/A=");
        lcd.print(stageTemp,0); //changed to 0 digit precision -mdw
        lcd.print(" ");  // added space as place holder for temp < 100 - mdw
        if (pumpRest==false)PID_HEAT(true);
      } 
      display_lcd(9,1,"/");
      lcd.print(Temp_f,1);  //was Temp_c, changed to 1 digit -mdw
      lcd.write((uint8_t)0);
    }
    if ((Temp_f >= stageTemp)&& (tempReached == false)){
      tempReached = true; //was 95c
      if (stage == 9) hop_add();  // check for hop add once temp reached
    }
    pump_rest(stage);
    if (stage==9){
      if(stageTime<lastminute){ //check for minute tick
        hop_add();  //check to add hops
      }
    }
    quit_mode (autoEnter);
  }
}



void get_stage_settings (void)
{
  stageTemp = word(EEPROM.read(tempHAddr),EEPROM.read(tempLAddr));
  stageTemp = stageTemp/16.0;
  if (resume){                 // on the start of resume gets saved time
    stageTime=EEPROM.read(37);
    resume = false;            // clears resume for next loop
  }
  else{
    stageTime = EEPROM.read(timeAddr); // gets stage time
    EEPROM.write(37,lowByte(stageTime));// saves the intial stage time
  } 
}



void add_malt (void)
{
  boolean malt;
  lcd.clear();
  pump_off();
  heat_off();
  display_lcd(0,0,"    Add Malt    ");
  Buzzer(3);
  display_lcd(0,1,"  Ok      Quit");
  wait_for_confirm(malt);
  if (malt==false){
    heat_off();
    pump_off();
    lcd.clear();
    delay(50);
    mainMenu=0;
    autoEnter = false;
  }
}



void remove_malt (void)
{
  boolean malt;
  lcd.clear();
  x = 9;               // used add to stage count on the final stage for the resume 
  EEPROM.write(36,lowByte(x)); // stores the stage number for the resume
  pump_off();
  heat_off();
  display_lcd(0,0,"  Remove  Malt  ");
  Buzzer(3);
  display_lcd(0,1,"  Ok      Quit");
  wait_for_confirm(malt);
  if (malt==false){
    stageTime = EEPROM.read(40);
    EEPROM.write(37,lowByte(stageTime));
    heat_off();
    pump_off();
    lcd.clear();
    delay(50);
    mainMenu=0;
    autoEnter = false;
  }
}



void get_boil_settings (void)
{
  nmbrHops = EEPROM.read(39);
  if (resume){
    if(x!=9){
      stageTime = EEPROM.read(40);
    }
    else{
      stageTime= EEPROM.read(37);
    }
  }
  else{
    stageTime = EEPROM.read(40);
    EEPROM.write(37,lowByte(stageTime));
  }
  hopAdd = EEPROM.read(49);
  blhpAddr = hopAdd+41;
  lcd.clear();
  hopTime = EEPROM.read(blhpAddr);
  boil_output = EEPROM.read(boilHeatAddr);
}



void manual_mode (void)
{
  load_pid_settings();
  boil_output = EEPROM.read(boilHeatAddr); // get boil heat %
  prompt_for_water();
  wait_for_confirm(manualLoop);
  while (manualLoop){            // manual loop
    Temperature();
    Setpoint = mset_temp;
    Input = Temp_f;   //was Temp_c -mdw
    display_lcd(2,0,"Manual Mode   ");
    quit_mode(manualLoop);
    heat_control();
    pump_control();
    if (Setpoint > 200 && Input >= Setpoint) {  //if temp reached during boil
      //  if (Input >= Setpoint) {  //**** remove after debug, uncomment line above
      change_up_dwn(boil_output,100,0);  // allow manual control of Output
      Output = boil_output;
      display_lcd(2,1,"%/A=");    //Display output%
      lcd.print(boil_output,0); //Display output%
      lcd.print(" "); //added space as place holder for <100
      if (mheat)
        PID_HEAT(false); //set heat in manual mode
    }
    else{
      display_lcd(2,1,"S/A=");
      lcd.print(mset_temp,0); //changed to 0 digit precision -mdw
      lcd.print(" ");  // added space as place holder for temp < 100 - mdw
      change_up_dwn(mset_temp,248,68);
      if (mheat)
        PID_HEAT(true); //set heat in auto mode
    }
    display_lcd(9,1,"/");
    lcd.print(Temp_f,1);  //changed to Temp_f and 1 decimal digit -mdw
    lcd.write((uint8_t)0);    
  }

}



void auto_mode (void)
{
  load_stage_settings();
  load_pid_settings();
  check_for_resume();
  if(!(resume)){  // if starting a new process prompt for water
    prompt_for_water();
    wait_for_confirm(autoEnter);
    if(!(autoEnter))return;
    hopAdd =0; //reset hop count at the start of the processes
    EEPROM.write(49,hopAdd); 
    pumpTime =0;
    pump_prime();
    x = 0;
  }
  if (autoEnter){     // mash steps
    EEPROM.write(35,1);// auto mode started
    if (twovessel) startdelayloop();
    if (!(autoEnter)) return;
    for (int i = x;i < nmbrStgs;i++){
      EEPROM.write(36,lowByte(x)); // stores the stage number for the resume
      x++;                         // used to count the stages for the resume 
      tempReached = false;
      get_stage_settings();
      start_time(); 
      stage_loop(i);
      if (!(autoEnter)) break;
      if( i==0 && autoEnter && !twovessel){    // at the end of the mashIn step pauses to add grain (single vessel only)
        add_malt();
        if (!(autoEnter))break;
      }
      if(i==(nmbrStgs-1)&& autoEnter){   // at the end of the last step pauses to remove the malt pipe before the boil
        remove_malt();
        if (!(autoEnter))break;
      }          
      Buzzer(1); 
      tempHAddr +=3; // increase stage addresses 
      tempLAddr +=3;
      timeAddr +=3;
      lcd.clear(); 
    }
  }
  // start of the boil
  if(autoEnter){
    start_time(); 
    stageTemp= boilStageTemp; // was set the intital boil temp to 98 deg c -mdw
    tempReached = false;  
    get_boil_settings();
    //   stage_loop(9,248,201);  //was 9,120,94
    stage_loop(9,248,70);  //***** allow lower temp for debug
    if(autoEnter){    // finishes the brewing process
      display_lcd(0,0,"    Brewing     ");
      display_lcd(0,1,"   Finished     ");
      Buzzer(3);
      delay(2000);
      EEPROM.write(35,0); // sets auto start byte to 0 for resume
      EEPROM.write(49,0); // sets hop count to 0
      mainMenu=0;
      autoEnter =false;
      resume =false;
    }


  }
}



void save_settings (int addr,int data)
{
  EEPROM.write(addr,highByte(data));
  EEPROM.write((addr+1),lowByte(data));
  /* Serial.print ("Addr =");
   Serial.print(addr);
   Serial.print("  Data word=");
   Serial.println(data); */
}  



void save_settings (int addr,byte data){

  EEPROM.write(addr,data);
  /*Serial.print ("Addr =");
   Serial.print(addr);
   Serial.print("  Data byte=");
   Serial.println(data);*/

}



int change_set(int& set_change,int upper_limit,int lower_limit,int step_size)
{
  // Increase set temp
  if (Button_repeat(Button_up)){
    set_change+=step_size;
    display_lcd(0,1,"                ");
  }
  if (set_change > upper_limit)set_change = upper_limit;
  // decrease temp
  if (Button_repeat(Button_dn))
  {
    set_change-=step_size;
    display_lcd(0,1,"                ");    
  }
  if ( set_change < lower_limit) set_change = lower_limit;
}



int change_set(byte& set_change,int upper_limit,int lower_limit,int step_size)
{
  // Increase set temp
  if (Button_repeat(Button_up)){
    set_change+=step_size;
    display_lcd(0,1,"                ");
  }
  if (set_change > upper_limit)set_change = upper_limit;
  // decrease temp
  if (Button_repeat(Button_dn))
    if (set_change >= step_size)
  {
    set_change-=step_size;
    display_lcd(0,1,"                ");    
  }
  else
  {  
    set_change = 0;
    display_lcd(0,1,"                ");
  }
}




void unit_set (void)
{
  int param[] ={
    100,-100,1,100,-100,1,100,-100,1,5000,500,500,9,1,1,8,0,1                                              }; 
  int a = 0;
  boolean pidLoop = false;
  int pidSet,setaddr;
  int windowSizeSet;
  char* setName[] ={
    "Kp = ","Ki = ","Kd = ","Windowsize= ","Num of Stages=","Num of Hops="                                              };
  setaddr = 0;
  for(int i=0;i<6;i++){
    if((i>=0) && (i<=3)){
      if (i==3) setaddr = 33;  
      pidSet=word(EEPROM.read(setaddr),EEPROM.read((setaddr+1)));
    }
    if (i==4)setaddr = 38;
    if((i>=4) && (i<6)){
      pidSet= EEPROM.read(setaddr);
    }
    pidLoop= true;
    display_lcd(0,1,"                ");
    while (pidLoop){
      display_lcd(0,1,setName[i]); 
      lcd.print(pidSet);
      change_set(pidSet,param[a],param[a+1],param[a+2]);
      quit_mode(pidLoop);
      if (!(pidLoop))i=6;
      if(Button_hold_press(Button_nxt)){
        if (i >= 4){
          save_settings(setaddr,lowByte(pidSet));
          pidLoop = false;
        }
        else{
          save_settings(setaddr,pidSet);
          pidLoop = false;
        }
      }
    }
    if (i>=4){
      setaddr+=1;
    }
    else{
      setaddr+=2;
    }
    a+=3;
  }
}



void set_stages (void)
{
  boolean autotempLoop = false;
  boolean autotimeLoop = false;
  tempHAddr = 6;
  tempLAddr = 7;
  timeAddr = 8;  
  float stgtmpSet;
  int stgtmpSetword;
  int stgtimSet;
  nmbrStgs = EEPROM.read(38);
  for (int i=0; i<nmbrStgs;i++){ // loops for the number of stages 
    stgtmpSet = word(EEPROM.read(tempHAddr),EEPROM.read(tempLAddr));
    stgtmpSet = stgtmpSet/16.0;
    autotempLoop = true;
    while (autotempLoop){  // loops for temp adjust
      display_lcd(0,1,stgName[i]);
      lcd.print("Temp=");
      lcd.print(stgtmpSet);  //** check -mdw
      quit_mode(autotempLoop);
      if (autotempLoop == false){
        return;
      }
      change_up_dwn(stgtmpSet,185,68);  //was 85,20 -mdw
      if (Button_hold_press(Button_nxt)){
        stgtmpSet = stgtmpSet*16;
        stgtmpSetword =word(stgtmpSet);
        save_settings(tempHAddr,stgtmpSetword);
        display_lcd(0,1,"                ");  
        autotempLoop = false; 
      }
    }
    autotimeLoop = true;
    stgtimSet = EEPROM.read(timeAddr);
    while (autotimeLoop){ // loops to adjust time setting
      display_lcd(0,1,stgName[i]);
      lcd.print(" time=");
      lcd.print(stgtimSet);
      quit_mode(autotimeLoop);
      if (autotimeLoop == false){
        return;
      }
      change_set(stgtimSet,120,0,1);
      if (Button_hold_press(Button_nxt)){
        save_settings(timeAddr,lowByte(stgtimSet));
        display_lcd(0,1,"                ");  
        autotimeLoop = false;
      }
    }
    tempHAddr+= 3;
    tempLAddr+= 3;
    timeAddr+= 3;
  }
}  



void set_hops (void)
{
  boolean hopLoop = false;
  blhpAddr = 40;
  byte hopSet;
  nmbrHops = EEPROM.read(39);
  nmbrHops+=1;
  for(int i =0;i<nmbrHops;i++){
    hopLoop = true;
    hopSet = EEPROM.read(blhpAddr);
    while (hopLoop){
      if (i==0){
        display_lcd(0,1,"Boil time = ");
        lcd.print(int (hopSet));
      }
      else{
        display_lcd(0,1,"Hop ");
        lcd.print(i);
        lcd.print(" time = ");
        lcd.print(int(hopSet));
      }
      quit_mode(hopLoop);
      if( hopLoop == false){
        return;
      }
      change_set(hopSet,180,0,1);
      if (Button_hold_press(Button_nxt)){
        save_settings(blhpAddr,hopSet);
        lcd.setCursor(0,1);
        lcd.print("                ");    
        hopLoop = false;
      }
    }
    blhpAddr+= 1;
  }
}



void set_boil_pcnt(void)
{
  boolean boil_loop= true;
  boil_pcnt = EEPROM.read(boilHeatAddr); 
  while (boil_loop)
  {
    display_lcd(0,1,"Boil Heat %=");
    lcd.print (boil_pcnt);
    change_set(boil_pcnt,100,0,1);
    if (Button_hold_press(Button_nxt)){
      save_settings(boilHeatAddr,boil_pcnt);
      lcd.setCursor(0,1);
      lcd.print("                ");
      boil_loop = false;    
    }
  }
}

// set the system mode for single vessel BIAB or two vessel w/ auto mash-in mash-out and delayed start
void set_twovessel(void)
{
  boolean twovessel_loop= true;
  twovessel = EEPROM.read(twovesselAddr); 
  while (twovessel_loop)
  {
    if (twovessel) {
      display_lcd(0,1,"Mode=TwoVessel ");
    }
    else {
      display_lcd(0,1,"Mode=BIAB      ");
    }
    change_set(twovessel,1,0,1);
    if (Button_hold_press(Button_nxt)){
      save_settings(twovesselAddr,twovessel);
      lcd.setCursor(0,1);
      lcd.print("                ");
      twovessel_loop = false;    
    }
  }
}

void set_startdelay(void)
{
  int hours, minutes;
  byte startdelay15;
  boolean startdelay_loop = true;
  startdelay15 = EEPROM.read(startdelay15Addr);
  while (startdelay_loop)
  {
    hours = (startdelay15 * 15)/60;
    minutes = (startdelay15 * 15) % 60;
    display_lcd(0,1,"DelayTime=");
    lcd.print (hours);
    display_lcd(12,1,":");
    lcd.print(minutes);
    change_set(startdelay15,48,0,1);
    if (Button_hold_press(Button_nxt)){
      save_settings(startdelay15Addr,startdelay15);
      lcd.setCursor(0,1);
      lcd.print("                ");
      startdelay_loop = false;  
    }
  }
}
void auto_set(void)
{
  set_stages();
  set_hops();
  set_boil_pcnt();
  set_twovessel();
  set_startdelay();

}



void setup_mode (void)
{
  byte setupMenu = 0;
  boolean setupLoop = true;
  while (setupLoop){
    switch (setupMenu){ // to select between PID and Auto menu
      case(0):
      display_lcd(0,0,"Unit Parameters ");
      display_lcd(0,1,"                ");
      quit_mode(setupLoop);
      if (Button_hold_press(Button_up))setupMenu = 1;
      if (Button_hold_press(Button_nxt))unit_set();  
      break;
      case(1):
      display_lcd(0,0," Auto Parameters");
      display_lcd(0,1,"                ");
      quit_mode(setupLoop);
      if (Button_hold_press(Button_dn))setupMenu = 0;
      if (Button_hold_press(Button_nxt))auto_set();
      break;
    }
  }

}   



void setup()
{
  // Start up the library
  Serial.begin(9600);
  lcd.begin(16,2);
  pinMode (Button_up,INPUT_PULLUP);
  pinMode (Button_dn,INPUT_PULLUP);
  pinMode (Button_prev,INPUT_PULLUP);
  pinMode (Button_nxt,INPUT_PULLUP);
  pinMode (Heat,OUTPUT);
  pinMode (Pump,OUTPUT);
  pinMode (Buzz,OUTPUT);
  windowStartTime = millis();
  //tell the PID to range between 0 and the full window size
  myPID.SetMode(AUTOMATIC);
  // write custom symbol to LCD
  lcd.createChar(0,degf);  // was degc - mdw
}




void loop()
{
  switch(mainMenu){
    case (1):
    display_lcd(0,0,"  Manual Mode   ");
    display_lcd(0,1,"                ");
    delay (1000);
    lcd.clear();
    manual_mode();
    mainMenu = 0;
    break;
    case (2): 
    display_lcd(0,0,"   Auto Mode    ");
    display_lcd(0,1,"                ");
    delay (1000);
    lcd.clear();
    auto_mode();
    mainMenu = 0;  
    break;
    case (3):   
    display_lcd(0,0,"   Setup Mode   ");
    display_lcd(0,1,"                ");
    delay (1000);
    lcd.clear();
    setup_mode();
    mainMenu = 0;    
    break;
  default:      
    heat_off();
    pump_off();
    Temperature();
    display_lcd(0,0," Mike's Brewery");
    display_lcd(0,1,"  Temp=");
    lcd.print(Temp_f,1);  //was Temp_c, changed to 1 digit  -mdw
    lcd.write((uint8_t)0);
    lcd.print("     ");
    if (Button_1sec_press(Button_dn))mainMenu = 1;
    if (Button_1sec_press(Button_prev))mainMenu = 2;
    if (Button_1sec_press(Button_nxt))mainMenu = 3;
    break;    
  }
}





























































